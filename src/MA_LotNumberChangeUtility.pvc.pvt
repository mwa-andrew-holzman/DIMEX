! ** Utility to change lot numbers for DIMEX
! ** @Author Andrew Holzman
! ** @Date 2018.1.17
! o=new("MA_LotNumberChangeUtility",%sys_ss);o'ChangeLots("C:\Users\martin\Desktop\DLLLotSerialNumbers.csv")

DEF CLASS "MA_LotNumberChangeUtility"

	FUNCTION ChangeLots(FilePath$)PROCESS
	FUNCTION LOCAL LogOpenFile(oObject,filePath$)LOG_OPEN_FILE
	FUNCTION LOCAL OpenFiles()OPEN_FILES

	LOCAL SO_SalesOrderDistFH
	LOCAL SO404_OrderTierDistFH
	LOCAL IM_ItemCostFH
	LOCAL IM404_ItemLocQtyFH

	LOCAL TierType$ = "3"

	! maybe?
	! LOCAL SO404_OrderAllocHeadFH
	! LOCAL SO404_OrderAllocDetFH
	! LOCAL IM_ItemLocUDFFH

END DEF
!
PROCESS:
	ENTER FilePath$
	_OBJ'OpenFiles()
	counter=0

	answer$ = %sys_ss'UI'MessageBox$("1","Run Utility?","style=YESNO,icon=!")
	IF (answer$ = "YES") {
		cCSVFH = HFN
		didOpen = 0
		mwaRetVal = _OBJ'LogOpenFile(cCSVFH,FilePath$)
		IF (mwaRetVal = 1) {
			DIM fileData$[12]
			counter=0
			READ(cCSVFH, ERR=*NEXT)line$ ! skip the first line of headers
			WHILE (1)
				READ(cCSVFH, END=*BREAK)line$
				READ DATA FROM line$,SEP="," TO fileData${all}
				currentSageLot$ = fileData$[0]
				comment$ = fileData$[1]
				serialNo$ = fileData$[2]
				timeStamp$ = fileData$[3]
				qty$ = fileData$[4]
				itemNo$ = fileData$[5]
				skidNo$ = fileData$[6]
				lotSerialNo$ = fileData$[7]
				binLocation$ = fileData$[8]
				whseCode$ = fileData$[9]
				newSageLotNo$ = fileData$[10]
				companyCode$ = fileData$[11]

				recFound=0
				READ(IM_ItemCostFH,REC=rec$,KEY=itemNo$:whseCode$:TierType$:currentSageLot$,DOM=*NEXT);recFound=1
				IF (recFound) {
					oldKey$ = KEC(IM_ItemCostFH)
					rec.GroupSort$ = serialNo$
					rec.LotSerialNo$ = serialNo$
					rec.UDF_FORMATTEDLOTNO$ = currentSageLot$
					WRITE(IM_ItemCostFH,REC=rec$)
					REMOVE(IM_ItemCostFH,KEY=oldKey$)
					%sys_ss'UI'MessageBox$("1","IM_ItemCost record modified")
				} ELSE {
					%sys_ss'UI'MessageBox$("1","Failed to locate IM_ItemCost record")
				}
				!
				counter=0
				SELECT *, REC=rec$ FROM SO_SalesOrderDistFH, KNO="kItemWarehouse" BEGIN itemNo$:whseCode$:currentSageLot$:$00$ END itemNo$:whseCode$:currentSageLot$:$FE$
					oldKey$ = KEC(OS_SalesOrderDistFH)
					rec.LotSerialNo$ = serialNo$
					WRITE(SO_SalesOrderDistFH,REC=rec$)
					REMOVE(SO_SalesOrderDistFH,KEY=oldKey$)
					counter++
				NEXT RECORD
				%sys_ss'UI'MessageBox$("1","SO_SalesOrderTierDistribution Records Affected: " + STR(counter))
				!
				counter=0
				SELECT *, REC=rec$ FROM SO404_OrderTierDistFH, KNO="kItem" BEGIN itemNo$:whseCode$:currentSageLot$:binLocation$:$00$ END itemNo$:whseCode$:currentSageLot$:binLocation$:$FE$
					oldKey$ = KEC(SO404_OrderTierDistFH)
					rec.LotSerialNo$ = serialNo$
					WRITE(SO404_OrderTierDistFH,REC=rec$)
					REMOVE(SO404_OrderTierDistFH,KEY=oldKey$)
					counter++
				NEXT RECORD
				%sys_ss'UI'MessageBox$("1","SO404_OrderTierDist Records Affected: " + STR(counter))
				!
				counter=0
				SELECT *, REC=rec$ FROM IM404_ItemLocQtyFH,KNO="kItemloc" BEGIN whseCode$:itemNo$:binLocation$:currentSageLot$:$00$ END whseCode$:itemNo$:binLocation$:currentSageLot$:$FE$
					oldKey$ = KEC(IM404_ItemLocQtyFH)
					rec.LotSerialNo$ = serialNo$
					WRITE(IM404_ItemLocQtyFH,REC=rec$)
					REMOVE(IM404_ItemLocQtyFH,KEY=oldKey$)
					counter++
				NEXT RECORD
				%sys_ss'UI'MessageBox$("1","IM404_BinLocationQty Records Affected: " + STR(counter))
				!
				! maybe?
				!SO404_OrderAllocHeadFH
				!SO404_OrderAllocDetFH
				!IM_ItemLocUDFFH

			WEND
		} ELSE {
			 %sys_ss'UI'MessageBox$("1","Failed to open the file")
		}
	} ELSE {
		 %sys_ss'UI'MessageBox$("1","Utility Cancelled")
	}
RETURN
!
LOG_OPEN_FILE:
	ENTER (oObject),(filePath$)
	Open(oObject,ERR=*NEXT)filePath$;mwaRetVal=1
	IF mwaRetVal=0 {
		_OBJ'LogMessage(2,"Failed to open file " + filePath$)
	}
RETURN mwaRetVal
!
OPEN_FILES:
	
	SO_SalesOrderDistFH = %sys_ss'OpenTable("SO_SalesOrderTierDistribution","COMPANY")
	SO404_OrderTierDistFH = %sys_ss'OpenTable("SO404_OrderTierDist","COMPANY")
	IM_ItemCostFH = %sys_ss'OpenTable("IM_ItemCost","COMPANY")
	IM404_ItemLocQtyFH = %sys_ss'OpenTable("IM404_ItemLocationQuantity","COMPANY")
	! maybe?
	! SO404_OrderAllocHeadFH = %sys_ss'OpenTable("SO404_OrderAllocationHeader","COMPANY")
	! SO404_OrderAllocDetFH = %sys_ss'OpenTable("SO404_OrderAllocationDetail","COMPANY")
	! IM_ItemLocUDFFH = %sys_ss'OpenTable("IM_ItemLocationUDF","COMPANY")

RETURN retSUCCESS
