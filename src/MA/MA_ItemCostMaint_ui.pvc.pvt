DEF CLASS "MA_ItemCostMaint_ui" CREATE REQUIRED DELETE REQUIRED
	LIKE "SY_MaintUI"

	FUNCTION LOCAL ChangeITEMCODE()CHANGE_ITEMCODE
	FUNCTION LOCAL ChangeWAREHOUSECODE()CHANGE_WAREHOUSECODE
	FUNCTION LOCAL ChangeLOTSERIALNO()CHANGE_LOTSERIALNO

	FUNCTION LOCAL ChangeML_UDF_WEIGHT()CHANGE_WEIGHT
	FUNCTION LOCAL ChangeML_UDF_SKIDNUM()CHANGE_SKIDNUM
	FUNCTION LOCAL ChangeML_UDF_LOT()CHANGE_LOT

	FUNCTION LOCAL BT_FindItem()BT_FIND_ITEM
	FUNCTION LOCAL BT_ACCEPT()BT_ACCEPT
	FUNCTION LOCAL BT_REGEN()BT_REGEN
	FUNCTION LOCAL BT_Cancel()BT_CANCEL

	LOCAL item$
	LOCAL warehouse$
	LOCAL sn$
	LOCAL cLotLookup$ 
END DEF 
!
ON_CREATE:
	ENTER coSession

	coBusiness=_OBJ'InitBusinessObject("MAIN","MA_ItemCostMaint_bus",$$,isTRUE,isTRUE,isTRUE)
	IF NOT(coBusiness) {
		EXIT errOBJECT
	}

	SCREEN_LIB$="MA_ItemCostMaint.M4L"
	SCREEN_ID$="DMAIN"

RETURN retSUCCESS
!
ON_DELETE:
RETURN retSUCCESS
!
BT_FIND_ITEM:
	IF (item$ = "") OR (warehouse$ = "") OR (sn$ = "") {
		coSession'UI'MessageBox$("1","Item, Warehouse, and LotSerialNo are required")
		mwaRetVal = retFAILURE
		RETURN mwaRetVal
	}
	mwaRetVal = coBusiness'SetKeyValue("ItemCode$",Item$)
	mwaRetVal = coBusiness'SetKeyValue("WarehouseCode$",Warehouse$)
	mwaRetVal = coBusiness'SetKeyValue("TierType$","3")
	mwaRetVal = coBusiness'SetKeyValue("GroupSort$",SN$)
	mwaRetVal = coBusiness'SetKey()
	
	coBusiness'GetValue("UDF_WEIGHT",weight)
	coBusiness'GetValue("UDF_SKIDNUM",skid)
	coBusiness'GetValue("UDF_LOT$",lot$)
	coBusiness'GetValue("UDF_FormattedLotNo$",fLotNo$)
	ML_UDF_LOT$ = lot$
	ML_UDF_SKIDNUM = skid
	ML_UDF_WEIGHT = weight
	ML_UDF_FORMATTEDLOTNO$ = fLotNo$

RETURN mwaRetVal
!
BT_ACCEPT:
	baseRet = _OBJ'BT_ACCEPT(FROM "SY_MaintUI")

	! update IM404_ItemLocationQuantity with potentially changed UDF_FormattedLotNo
	mbLocFH = coSession'OpenTable("IM404_ItemLocationQuantity","COMPANY")
	!"ItemCode:WarehouseCode:LotSerialNo:BinLocation:PalletID"
	SELECT *, REC=lRec$ FROM mbLocFH BEGIN item$:warehouse$:sn$:$$ END item$:warehouse$:sn$:$FE$
		lRec.UDF_FormattedLotNo$ = ML_UDF_FORMATTEDLOTNO$
		WRITE(mbLocFH,REC=lRec$)
	NEXT RECORD

	! clear UI fields
	ML_UDF_LOT$ = ""
	ML_UDF_SKIDNUM = 0
	ML_UDF_WEIGHT = 0
	ML_UDF_FORMATTEDLOTNO$ = ""
RETURN baseRet
!
CHANGE_ITEMCODE:
	item$ = ITEMCODE$
	cLotLookup$ = pad(item$,30,$00$)+pad(warehouse$,3,$00$)+"3"
RETURN retSUCCESS
!
CHANGE_WAREHOUSECODE:
	warehouse$ = WAREHOUSECODE$
	cLotLookup$ = pad(item$,30,$00$)+pad(warehouse$,3,$00$)+"3"
RETURN retSUCCESS
!
CHANGE_LOTSERIALNO:
	sn$ = LotSerialNo$
RETURN retSUCCESS
!
CHANGE_WEIGHT:
	mwaRetVal = coBusiness'SetValue("UDF_WEIGHT",ML_UDF_WEIGHT)
RETURN mwaRetVal
!
CHANGE_SKIDNUM:
	mwaRetVal = coBusiness'SetValue("UDF_SKIDNUM",ML_UDF_SKIDNUM)
RETURN mwaRetVal
!
CHANGE_LOT:
	mwaRetVal = coBusiness'SetValue("UDF_lot$",ML_UDF_LOT$)
RETURN mwaRetVal
!
BT_REGEN:
	newLot$ = PAD(ML_UDF_LOT$,6,0,"0") + PAD(STR(ML_UDF_SKIDNUM),3,0,"0") + PAD(STR(ML_UDF_WEIGHT),6,0,"0")
	mwaRetVal = coBusiness'SetValue("UDF_FormattedLotNo$",newLot$)
	ML_UDF_FORMATTEDLOTNO$ = newLot$
RETURN mwaRetVal
!
BT_CANCEL:
	ML_UDF_LOT$ = ""
	ML_UDF_SKIDNUM = 0
	ML_UDF_WEIGHT = 0
	ML_UDF_FORMATTEDLOTNO$ = ""

	item$ = ""
	warehouse$ = ""
	sn$ = ""

	baseRet = _OBJ'BT_Cancel(FROM "SY_MaintUI")
RETURN baseRet