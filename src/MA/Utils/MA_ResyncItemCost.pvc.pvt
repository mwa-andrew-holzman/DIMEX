!o=new("MA_ResyncItemCost",%sys_ss);o'ResyncItemCost()
DEF CLASS "MA_ResyncItemCost"

	LOCAL POSConnServer$="NTServer18"
	LOCAL POSConnDatabase$="FinishedGoods"
	LOCAL POSConnUsername$="SA"
	LOCAL POSConnPassword$="71779121375"
	LOCAL SQLSchema$="dbo"

	LOCAL memFH

	FUNCTION ResyncItemCost(Company$)RESYNC_ITEM_COST
	FUNCTION FixLocQty()PROCESS_LOCQTY
	FUNCTION LOCAL OpenConn(Server$,Database$,Username$,Password$)OPEN_CONN
	FUNCTION LOCAL DeleteObject(oObject)DELETE_OBJECT
	FUNCTION LOCAL FreeSQLQuery(oObject)FREE_SQL_QUERY

END DEF
!
RESYNC_ITEM_COST:
	ENTER Company$
	itemCostFH = %sys_ss'OpenTable("IM_ItemCost","COMPANY")
	! run a query to build a table of all records where the UDFs in ItemCost do match what they should in FG_InventoryLog
	! store in memfh and then compare item cost to memfh
	CONN=_OBJ'OpenConn(POSConnServer$, POSConnDatabase$, POSConnUsername$, POSConnPassword$)
	
	IF NOT(CONN)  { RETURN retFAILURE }

	IF (Company$ = "DLL") {
		SQLCommand$ = "EXEC dbo.spSageMismatchedLotsDLL"
	}
	IF (Company$ = "DOP" ) {
		SQLCommand$ = "EXEC dbo.spSageMismatchedLotsDOP"
	}
	IF (Company$ = "DCA" ) {
		SQLCommand$ = "EXEC dbo.spSageMismatchedLotsDCA"
	}

	FGInvRS = CONN'EXECUTE(SQLCommand$)
	IF (FGInvRS'EOF) {
		_OBJ'LogMessage(2,"No Invoice Records Found")
		coSession'UI'ProgressBar("close")
		mwaRetVal = 0
	}
	counter=0
	WHILE NOT(FGInvRS'EOF)
		ItemCode$ = FGInvRS'Fields("ItemCode")'Value$
		WarehouseCode$ = FGInvRS'Fields("WarehouseCode")'Value$
		TierType$ = FGInvRS'Fields("TierType")'Value$
		GroupSort$ = FGInvRS'Fields("GroupSort")'Value$
		UDF_FormattedLotNo$ = FGInvRS'Fields("UDF_FormattedLotNo")'Value$
		CorrectLotNo$ = FGInvRS'Fields("CorrectLotNo")'Value$

		READ(itemCostFH,REC=costRec$,KEY=ItemCode$:WarehouseCode$:TierType$:GroupSort$,ERR=*NEXT);recFound=1
		IF (recFound) {
			! on a matching record to the table. double check its UDF does not match the tables CorrectUDF value and then set it and write
			IF (costRec.UDF_FormattedLotNo$ <> CorrectLotNo$) {
				costRec.UDF_FormattedLotNo$ = CorrectUDF$
				WRITE(itemCostFH,REC=costRec$);writeCount++
			}
		}
		FGInvRS'MoveNext()
	WEND



	!MSGBOX("Utility Finished")
	_OBJ'FreeSQLQuery(FGInvRS)
	_OBJ'DeleteObject(CONN)
	CLOSE(itemCostFH); itemCostFH = 0

RETURN writeCount
!!!!!!!!!
PROCESS_LOCQTY:
	!loop through im_itemwarehouse
	! set the udf_formattedlotno on im404_itemlocationqty to the value of im_itemwarehouse udf_formattedlotno

	itemCostFH = %sys_ss'OpenTable("IM_ItemCost","COMPANY")
	itemLocFH = %sys_ss'OpenTable("IM404_ItemLocationQuantity","COMPANY")
	SELECT *, REC=rec$ FROM itemLocFH BEGIN $$ END $FE
		validRead=0
		READ(itemCostFH,REC=costRec$,KEY=rec.ItemCode$:rec.WarehouseCode$:"3":rec.LotSerialNo$,ERR=*NEXT);validRead=1
		IF (rec.UDF_FormattedLotNo$ <> costRec.UDF_FormattedLotNo$) {
			rec.UDF_FormattedLotNo$ = costRec.UDF_FormattedLotNo$
			WRITE(itemLocFH,REC=rec$);writeCounter++
		}
	NEXT RECORD 
RETURN writeCounter
!!!!!!!!!
OPEN_CONN:
	ENTER (Server$),(Database$),(Username$),(Password$)
	DEF OBJECT CONN,"ADODB.Connection"
	ConnStr$="DRIVER={SQL Server};SERVER="+Server$+";UID="+Username$+";PWD="+Password$+";DATABASE="+Database$
	CONN'ConnectionString$=ConnStr$
	CONN'Provider$="sqloledb.1"
	CONN'CommandTimeout=60
	mwaRetVal=0; CONN'OPEN(ConnStr$,ERR=*NEXT); mwaRetVal=1
	IF NOT(mwaRetVal) { _OBJ'LastErrorMsg$="Failed to open connection"; RETURN retFAILURE }
RETURN CONN

DELETE_OBJECT:
	ENTER oObject
	IF oObject { DROP OBJECT oObject; oObject=0 }
RETURN retSUCCESS
!
FREE_SQL_QUERY:
	ENTER oQuery
	IF oQuery {oQuery'PVXFREE() }
RETURN retSUCCESS